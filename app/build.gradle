plugins {
    id 'application'
    id 'io.freefair.lombok' version '5.1.0'
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id 'org.beryx.runtime' version '1.12.4'
}

repositories {
    jcenter()
}

ext {
    gdxVersion = '1.9.10'
    daggerVersion = '2.28.1'
}

dependencies {
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation 'com.badlogicgames.ashley:ashley:1.7.3'

    implementation 'org.apache.logging.log4j:log4j-api:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'

    implementation "com.google.dagger:dagger:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'
}

sourceSets {
    main {
        java {
            srcDirs += 'build/generated/sources/annotationProcessor/java/main'
        }
    }
}

application {
    mainClassName = 'chrome.dinosaur.ChromeDinosaur'
}

run {
    ignoreExitValue = true
    jvmArgs '--illegal-access=deny'
}

test {
    useJUnitPlatform()
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    modules = ['java.base', 'java.compiler', 'java.datatransfer', 'java.desktop', 'java.logging', 'java.management',
        'java.naming', 'java.rmi', 'java.scripting', 'java.sql', 'java.xml', 'jdk.unsupported']
}

shadowJar {
    archiveBaseName.set(rootProject.name)
    archiveClassifier.set('')
    archiveVersion.set('')
}

sourceCompatibility = 11
targetCompatibility = 11